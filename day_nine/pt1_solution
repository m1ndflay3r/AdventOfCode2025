#!/usr/bin/env zsh

## env setup

# read input into array, err if not found
[ ! -f input ] && {
  print "Error: \'input\' not found."
  return 1
}
typeset -a raw_lines
raw_lines=( ${(f)"$(<input)"} )

## parse steps

typeset -a X Y
integer i=0

for line in $raw_lines; do
  # skip empty lines if present
  [[ -z "$line" ]] && continue
  ((i++))
  # separate coord into x and y
  X[i]=${line%,*}
  Y[i]=${line#*,}
done

## main

integer count=$#X
integer max_area=0
integer idx_a idx_b
integer w h area
integer xa ya xb yb

print "Processing $count coordinates..."

for ((idx_a=1; idx_a<count; idx_a++)); do
  xa=$X[idx_a]
  ya=$Y[idx_a]

  # inner loop starts at idx_a + 1 so we dont double-process any pairs
  for ((idx_b=idx_a+1; idx_b<=count; idx_b++)); do
    xb=$X[idx_b]
    yb=$Y[idx_b]
    # calculate absolute difference + 1 for tiles
    ((w = (xa > xb ? xa - xb : xb - xa) + 1))
    ((h = (ya > yb ? ya - yb : yb - ya) + 1))
    ((area = w * h))
    # update max_area if current is larger
    ((area>max_area)) && max_area=$area
  done
done

print "Largest area: $max_area"
